buildscript {
    ext {
        springBootVersion = '1.5.14.RELEASE'
        kotlinVersion = '1.2.51'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'au.com.anz'
sourceCompatibility = 1.8

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

compileJava.dependsOn(processResources)

repositories {
    mavenCentral()
}

processResources {
    logger.info "Processing resources with tokens VERSION: $project.app_version-$project.app_release " +
            "PROJECT_NAME: $project.name"
    filter org.apache.tools.ant.filters.ReplaceTokens, tokens: [VERSION     : project.app_version,
                                                                RELEASE     : project.app_release,
                                                                PROJECT_NAME: project.name]
}

sourceSets {
    main {
        java.srcDirs = ['src/main/java']
        kotlin.srcDirs = ['src/main/java', 'src/main/kotlin']
        resources.srcDirs = ['src/main/resources']
    }
}

dependencies {
    compile("org.apache.commons:commons-lang3:3.7")
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-aop')
    compile('org.springframework.cloud:spring-cloud-starter-config:1.4.4.RELEASE')
    compile('com.fasterxml.jackson.module:jackson-module-kotlin')
    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    compile("org.jetbrains.kotlin:kotlin-reflect")
    compile("io.springfox:springfox-swagger2:2.9.2")
    compile("io.springfox:springfox-swagger-ui:2.9.2")

//    compile('org.apache.logging.log4j:log4j-core:2.11.0')
//    compile('org.apache.logging.log4j:log4j-api:2.11.0')
//    compile('org.apache.logging.log4j:log4j-slf4j-impl:2.11.0')
    compile('io.github.microutils:kotlin-logging:1.5.4')
    compile('com.auth0:java-jwt:3.4.0')

    compileOnly("org.springframework.boot:spring-boot-configuration-processor")

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile "org.codehaus.groovy:groovy-all:2.4.13"
    testCompile "org.spockframework:spock-core:1.1-groovy-2.4"
    testCompile "org.spockframework:spock-spring:1.1-groovy-2.4"
}

configurations.all {
    exclude group: 'ch.qos.logback'
}


bootRun {
    args = ['--spring.config.location=classpath:env.local.yml']
}


jar {
    baseName = project.name
    excludes = ['**/env.*.yml', '**/*.json']
    archiveName = 'bankservice.jar'

    manifest {
        attributes 'Built-Date': new Date() //now
        attributes 'Built-By': System.getProperty('user.name')
        attributes 'Build-Jdk': System.getProperty('java.version')
        attributes 'Implementation-Title': project.name
        attributes 'Implementation-Version': project.version
        attributes 'Implementation-Vendor-Id': project.group
        attributes 'Main-Class': 'au.com.anz.BankApplication'
    }
}
